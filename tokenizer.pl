/** <module> Logical English Tokenizer
    
    This module provides predicates for converting Logical English source text
    into a list of tokens. It handles indentation, words, numbers, dates,
    quoted strings, and comments. It also provides a way to convert tokens
    back into a string.
*/

:- module(tokenizer,[tokenize/2, tokenize/4, tokenize_lang/2, tokenize_file/2, tokens_to_string/2]).

:- use_module(library(dcg/basics)).

% Number-locale state for the current tokenization run: the decimal separator
% and thousands separator codes (English: '.' and ','; Portuguese/Spanish/
% French/Italian: ',' and '.'). Set by tokenize/4; plain tokenize/2 uses the
% English/ISO defaults, so existing callers are unaffected.
:- thread_local current_num_seps/2.

num_seps(Dec, Thou) :-
    ( current_num_seps(D, T) -> Dec = D, Thou = T ; Dec = 0'., Thou = 0', ).

%!  tokenize_file(+File:atom, -Tokens:list) is det.
%
%   Reads the content of File and converts it into a list of tokens.
tokenize_file(File,Tokens) :-
    read_file_to_string(File, String, []),
    tokenize(String, Tokens).

%!  tokenize(+String:string, -Tokens:list) is det.
%
%   Converts a string into a list of Logical English tokens, using the
%   English/ISO number locale ('.' decimal point, ',' thousands separator).
tokenize(String, Tokens) :-
    tokenize(String, '.', ',', Tokens).

%!  tokenize(+String, +DecimalSep:atom, +ThousandsSep:atom, -Tokens) is det.
%
%   As tokenize/2 with an explicit number locale. In comma-decimal languages
%   (pt/es/fr/it: DecimalSep = ',', ThousandsSep = '.') a comma DIRECTLY
%   between digits is a decimal separator ("o custo é 1,5") — so list and
%   argument commas there should be followed by a space ("[1, 5]"). A '.'
%   thousands separator groups exactly three digits ("1.234.567"), never
%   clashing with the sentence-final full stop (which is not digit-digit).
tokenize(String, DecSep, ThouSep, Tokens) :-
    atom_codes(DecSep, [DecCode]),
    atom_codes(ThouSep, [ThouCode]),
    string_codes(String, Codes),
    setup_call_cleanup(
        ( retractall(current_num_seps(_, _)),
          assertz(current_num_seps(DecCode, ThouCode)) ),
        phrase(tokens(0, 1, Tokens0), Codes),
        retractall(current_num_seps(_, _))
    ),
    negative_numbers(Tokens0, none, Tokens).

%!  negative_numbers(+Tokens0, +Previous, -Tokens) is det.
%
%   A minus sign directly followed by a number ("-5", "-2.5") is a negative
%   number when the sign is not glued to a preceding word or number: after
%   the start of a line, a space, an operator or an opening bracket ("has
%   -5 degrees", ">= -2", "[-1, 2]"), but not in "ICD-10", "3-5" or "x-1".
%   Binary subtraction is written with spaces ("the amount - 5"), which this
%   leaves alone because the sign is not attached to the number.
negative_numbers([], _, []).
negative_numbers([T|Rest0], Prev, [T1|Out]) :-
    (   T = punctuation(-, loc(S, E)),
        Rest0 = [number(N, loc(E, E2))|Rest1],
        unary_minus_position(Prev, S)
    ->  M is -N, T1 = number(M, loc(S, E2)), Rest = Rest1
    ;   T1 = T, Rest = Rest0
    ),
    negative_numbers(Rest, T1, Out).

unary_minus_position(none, _) :- !.
unary_minus_position(indent(_, _), _) :- !.
unary_minus_position(punctuation(P, _), _) :- !, \+ memberchk(P, [')', ']']).
unary_minus_position(Prev, S) :-
    arg(2, Prev, loc(_, PrevEnd)),
    PrevEnd < S.

%!  tokenize_lang(+String, -Tokens) is det.
%
%   Tokenizes with the number locale of the ACTIVE language (le_i18n).
tokenize_lang(String, Tokens) :-
    (   catch(le_i18n:le_active_language(Lang), _, fail),
        catch(le_i18n:language_param(Lang, decimal_sep, Dec), _, fail),
        catch(le_i18n:language_param(Lang, thousands_sep, Thou), _, fail),
        Dec \== '', Thou \== ''
    ->  tokenize(String, Dec, Thou, Tokens)
    ;   tokenize(String, Tokens)
    ).

%!  tokens_to_string(+Tokens:list, -String:string) is det.
%
%   Converts a list of tokens back into a string representation,
%   preserving relative spacing and indentation.
tokens_to_string([],"") :- !.
tokens_to_string([T|Ts],String) :-
    ( arg(2, T, loc(Start, _)) -> true ; Start = 0 ),
    tokens_to_string_([T|Ts],Start,Strings),
    atomic_list_concat(Strings,String).

%!  leading_zeros_width(+N, +Begin, +End, -Width) is semidet.
%
%   The number N was written in the span Begin-End with leading zeros (`01`,
%   `007`): Width is the number of digits written. A span that is as long as
%   N written with thousands separators (`1,000`) is not.
leading_zeros_width(N, Begin, End, Width) :-
    integer(N), N >= 0, integer(Begin), integer(End),
    Width is End - Begin,
    format(atom(A), '~d', [N]),
    atom_length(A, L),
    Width > L,
    Width =\= L + (L - 1) // 3.

% tokens_to_string_(Tokens,EndPositionOfPrevious,Strings)
tokens_to_string_([],_,[]).
tokens_to_string_([T|Tokens],LastEnd,[S|Strings]) :-
    ( arg(2,T,loc(Begin,NewEnd)) ->
        Gap is Begin-LastEnd,
        (Gap > 0 -> Advance = " " ; Advance = ""),
        (   T=indent(_,_) -> S_ = "", Advance_ = Advance
            ; T=line_comment(_,_) -> S_ = "", Advance_ = Advance
            ; T=multi_comment(_,_) -> S_ = "", Advance_ = Advance
            ; T=quoteString(X,_) -> format(string(S_),"'~a'",[X]), Advance_ = Advance
            ; T=doubleQuoteString(X,_) -> format(string(S_),'"~a"',[X]), Advance_ = Advance
            ; T=var(Words,_) -> 
                atomic_list_concat(Words, ' ', WordsStr),
                format(string(S_), "*~w*", [WordsStr]),
                Advance_ = Advance
            ; arg(1,T,X) -> 
                ( X = date(Y,M,D) -> format(string(S_), "~w-~|~`0t~w~2+-~|~`0t~w~2+", [Y,M,D])
                  % a number written with leading zeros keeps them: the `01`
                  % of the claim reference SYN-01-C1 (see le_grammar's
                  % name_part_word/2)
                ; leading_zeros_width(X, Begin, NewEnd, Len)
                  -> format(string(S_), '~|~`0t~d~*+', [X, Len])
                ; (atom(X); string(X); number(X)) -> S_=X
                ; term_string(X, S_)
                ),
                Advance_ = Advance
            ; S_ = "", Advance_ = ""
        ),
        atomic_list_concat([Advance_,S_],S),
        tokens_to_string_(Tokens,NewEnd,Strings)
    ;   % Fallback for tokens without location info
        ( arg(1,T,X) -> S_ = X ; S_ = "" ),
        ( Strings == [] -> S = S_ ; atomic_list_concat([' ', S_], S) ),
        tokens_to_string_(Tokens, LastEnd, Strings)
    ).


% --- The Main DCG Loop ---

tokens(_, _, []) --> [].

% 1. Handle Newlines: Reset LineStart flag
% Support Windows (\r\n), Unix (\n), and old Mac (\r) line endings
tokens(Idx, _, Ts) -->
    "\r\n", !,
    { NewIdx is Idx + 2 },
    tokens(NewIdx, 1, Ts).
tokens(Idx, _, Ts) -->
    "\n", !,
    { NewIdx is Idx + 1 },
    tokens(NewIdx, 1, Ts).
tokens(Idx, _, Ts) -->
    "\r", !,
    { NewIdx is Idx + 1 },
    tokens(NewIdx, 1, Ts).

% 2. Indent: Triggered only at LineStart
tokens(Idx, 1, [indent(VW, loc(Idx, End))|Ts]) -->
    white_prefix(VW, CC),
    { End is Idx + CC },
    tokens(End, 0, Ts).

% 3. SKIP WHITESPACE (Space/Tab)
tokens(Idx, 0, Ts) -->
    [C], { code_type(C, white) }, !,
    { NewIdx is Idx + 1 },
    tokens(NewIdx, 0, Ts).

% 4. Identify specific tokens (Comments, Dates, Words, Numbers)
% Because we skipped whitespace in step 3, we are now looking exactly at the start of a token.
tokens(Idx, 0, [T|Ts]) -->
    token_match(Idx, T, NextIdx), !,
    tokens(NextIdx, 0, Ts).

% 5. FALLBACK: Any other character as punctuation
tokens(Idx, 0, [punctuation(A, loc(Idx, End))|Ts]) -->
    [C], !,
    { atom_codes(A, [C]), End is Idx + 1 },
    tokens(End, 0, Ts).

% --- Token Definitions (Priority Order) ---

% Multi-character Punctuation
token_match(Idx, punctuation(A, loc(Idx, End)), End) -->
    ">=", !, { A = '>=', End is Idx + 2 }.
token_match(Idx, punctuation(A, loc(Idx, End)), End) -->
    "<=", !, { A = '<=', End is Idx + 2 }.
token_match(Idx, punctuation(A, loc(Idx, End)), End) -->
    "=<", !, { A = '=<', End is Idx + 2 }.
token_match(Idx, punctuation(A, loc(Idx, End)), End) -->
    "==", !, { A = '==', End is Idx + 2 }.
token_match(Idx, punctuation(A, loc(Idx, End)), End) -->
    "!=", !, { A = '!=', End is Idx + 2 }.

% Multi-line Comment: /* ... */
token_match(Idx, multi_comment(Content, loc(Idx, End)), End) -->
    "/*", !,
    string_until_ending("*/", Codes),
    % "*/",
    { string_codes(Content, Codes),
      length(Codes, L),
      End is Idx + L + 4 }.

% Single-line Comment: % ...
token_match(Idx, line_comment(Content, loc(Idx, End)), End) -->
    "%", !,
    string_until_newline(Codes),
    { string_codes(Content, Codes),
      length(Codes, L),
      End is Idx + L + 1 }.

% Date: YYYY-MM-DD
token_match(Idx, date(date(Y, M, D), loc(Idx, End)), End) -->
    digits_strict(Yc), "-", digits_strict(Mc), "-", digits_strict(Dc), !,
    { number_codes(Y, Yc), number_codes(M, Mc), number_codes(D, Dc),
      length(Yc, Ly), length(Mc, Lm), length(Dc, Ld),
      End is Idx + Ly + Lm + Ld + 2 }.

% Quoted String: "..."
token_match(Idx, doubleQuoteString(S, loc(Idx, End)), End) -->
    "\"", !,
    string_until_ending("\"", Codes),
    { string_codes(S, Codes),
      length(Codes, L),
      End is Idx + L + 2 }.

% Quoted String: '...'
token_match(Idx, quoteString(S, loc(Idx, End)), End) -->
    "'", !,
    string_until_ending("'", Codes),
    { string_codes(S, Codes),
      length(Codes, L),
      End is Idx + L + 2 }.

% Number
% Accepts an optional integer part with thousands separators (e.g. 10,000,000),
% which are stripped from the numeric value but counted towards the token length.
token_match(Idx, number(N, loc(Idx, End)), End) -->
    digits_strict(Lead),
    thousands_groups(GroupCodes, SepCount),
    { append(Lead, GroupCodes, IntCodes) },
    { num_seps(DecCode, _) },
    (   decimal_part(DecCode, Fraction) ->
        { append(IntCodes, [0'.|Fraction], AllCodes),
          number_codes(N, AllCodes),
          length(IntCodes, IL), length(Fraction, FL),
          End is Idx + IL + SepCount + 1 + FL }
        ;
        { number_codes(N, IntCodes),
          length(IntCodes, IL),
          End is Idx + IL + SepCount }
    ).


% Word
token_match(Idx, word(A, loc(Idx, End)), End) -->
    [C], { code_type(C, alpha) }, !,
    word_remainder(Rest),
    { atom_codes(A, [C|Rest]),
      length([C|Rest], L),
      End is Idx + L }.

% decimal_part(+DecCode, -Fraction): the decimal separator directly followed
% by digits.
decimal_part(DecCode, Fraction) -->
    [DecCode],
    digits_strict(Fraction).

% --- Helpers ---

% Match characters, consuming the delimiter
string_until_ending(Delimiter, []) --> Delimiter, !.
string_until_ending(Delimiter, [C|Cs]) --> [C], string_until_ending(Delimiter, Cs).

string_until_newline([]) --> peek_newline, !.
string_until_newline([]) --> eos, !.
string_until_newline([C|Cs]) --> [C], string_until_newline(Cs).

% Lookahead for newline without consuming
peek_newline, [10] --> [10], !.
peek_newline, [13] --> [13], !.

digits_strict([C|Cs]) --> [C], { code_type(C, digit) }, !, digits_maybe(Cs).
digits_maybe([C|Cs])  --> [C], { code_type(C, digit) }, !, digits_maybe(Cs).
digits_maybe([])      --> [].

% Thousands separators: zero or more groups of a comma followed by exactly
% three digits (e.g. ",000"). A group only matches when those three digits are
% NOT immediately followed by another digit, so e.g. "1,2345" stays as the
% number 1 followed by a comma, never an invalid grouping. The collected codes
% exclude the commas; SepCount counts the commas consumed.
thousands_groups(Codes, Count) -->
    thousand_group(Group), !,
    thousands_groups(Rest, Count0),
    { append(Group, Rest, Codes), Count is Count0 + 1 }.
thousands_groups([], 0) --> [].

thousand_group([D1,D2,D3]) -->
    { num_seps(_, ThouCode) },
    [ThouCode], digit_code(D1), digit_code(D2), digit_code(D3),
    not_followed_by_digit.

digit_code(C) --> [C], { code_type(C, digit) }.

% Lookahead: succeeds (without consuming) at end of input or when the next
% character is not a digit. The pushback re-publishes the peeked character.
not_followed_by_digit, [C] --> [C], { \+ code_type(C, digit) }, !.
not_followed_by_digit --> eos.

white_prefix(VW, CC) --> [C], { code_type(C, white), C \== 10, C \== 13 }, !, 
    { ( C == 9 -> VInc = 8; VInc = 1), CInc = 1 },
    white_prefix(VW1, CC1), { VW is VW1 + VInc, CC is CC1 + CInc }.
white_prefix(0, 0) --> [].

%!  word_char(+Code) is semidet.
%
%   A character that continues a word. `csym` is the answer — letters, digits
%   and the underscore — and on a server with a UTF-8 locale it is the whole
%   answer.
%
%   `alnum` is here for the ones where it is not. code_type/2 classifies
%   `csym` through the C library's LC_CTYPE, and two builds do not have a
%   useful one: a server started under LANG=C (le_i18n:ensure_utf8_ctype
%   warns about it) and the WebAssembly build, whose C library has no locales
%   at all. In both, 'ã' is not `csym`, so "são" tokenized as "s", "ã", "o"
%   and a perfectly well formed Portuguese program came back malformed.
%   `alnum` and `alpha` are classified from SWI-Prolog's own Unicode tables
%   instead, and are right everywhere.
%
%   This widens nothing where the locale works: alnum is csym without the
%   underscore, so the second clause can only add characters the first one
%   should have matched and did not.
word_char(C) :- code_type(C, csym), !.
word_char(C) :- code_type(C, alnum).

word_remainder([C|Cs]) --> [C], { word_char(C) }, !, word_remainder(Cs).
% A lone apostrophe (no matching quote before the end of the line) attaches to the
% word, so templates may contain possessives/contractions, e.g. "employers'",
% "don't". At most one apostrophe per word; a quote that has a partner ahead on
% the line is left alone, so it still opens a string constant. (Code 39 = ').
word_remainder([39|Cs]) -->
    [39], peek_rest(After), { \+ quote_before_eol(After) }, !,
    word_remainder_no_quote(Cs).
word_remainder([])     --> [].

% Like word_remainder, but never absorbs a further apostrophe: this caps a word at
% a single apostrophe, keeping a second quote available to delimit a string.
word_remainder_no_quote([C|Cs]) --> [C], { word_char(C) }, !, word_remainder_no_quote(Cs).
word_remainder_no_quote([])     --> [].

% Lookahead: unify Rest with the remaining input without consuming anything.
peek_rest(Rest, Rest, Rest).

% True if a single quote occurs in Codes before the end of the current line.
quote_before_eol([39|_]) :- !.
quote_before_eol([10|_]) :- !, fail.
quote_before_eol([13|_]) :- !, fail.
quote_before_eol([_|Cs]) :- quote_before_eol(Cs).

spaces(N) --> {nonvar(N), N>0, N1 is N-1}, " ", !, spaces(N1).
spaces(N) --> {var(N)}, " ", !, spaces(N1), { N is N1 + 1 }.
spaces(0) --> "".

spaces(N,Spaces) :-
    spaces(N,Spaces_,[]),
    atom_codes(Spaces,Spaces_).
